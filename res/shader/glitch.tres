[gd_resource type="ShaderMaterial" load_steps=3 format=2]

[ext_resource path="res://res/spr/static/rgb.png" type="Texture" id=1]

[sub_resource type="Shader" id=1]
code = "shader_type canvas_item;

uniform bool enable_glitch = true;
uniform bool transparency_to_black = true;
uniform float block_thresh_modifier = 0.2;
uniform float line_thresh_modifier = 0.7;
uniform float block_dist_modifier = 0.35;
uniform sampler2D iChannel1;

void fragment(){
	if (enable_glitch){
		vec2 uv = UV.xy;
		vec2 block = floor(FRAGCOORD.xy / vec2(16));
		vec2 uv_noise = block / vec2(64);
		uv_noise += floor(vec2(TIME) * vec2(1234.0, 3543.0)) / vec2(64);
		
		float block_thresh = pow(fract(TIME * 1236.0453), 2.0) * block_thresh_modifier;
		float line_thresh = pow(fract(TIME * 2236.0453), 3.0) * line_thresh_modifier;
		
		vec2 uv_r = uv, uv_g = uv, uv_b = uv;
	
		// glitch some blocks and lines
		if (texture(iChannel1, uv_noise).r < block_thresh ||
			texture(iChannel1, vec2(uv_noise.y, 0.0)).g < line_thresh) {
	
			vec2 dist = (fract(uv_noise) - 0.5) * block_dist_modifier;
			uv_r += dist * 0.1;
			uv_g += dist * 0.2;
			uv_b += dist * 0.125;
		}
	
		COLOR.r = texture(TEXTURE, uv_r).r;
		COLOR.g = texture(TEXTURE, uv_g).g;
		COLOR.b = texture(TEXTURE, uv_b).b;
	
		// loose luma for some blocks
		if (texture(iChannel1, uv_noise).g < block_thresh)
			COLOR.rgb = COLOR.ggg;
	
		// discolor block lines
		if (texture(iChannel1, vec2(uv_noise.y, 0.0)).b * 3.5 < line_thresh)
			COLOR.rgb = vec3(0.0, dot(COLOR.rgb, vec3(1.0)), 0.0);
	
		// interleave lines in some blocks
		if (texture(iChannel1, uv_noise).g * 1.5 < block_thresh ||
			texture(iChannel1, vec2(uv_noise.y, 0.0)).g * 2.5 < line_thresh) {
			float line = fract(FRAGCOORD.y / 3.0);
			vec3 mask = vec3(3.0, 0.0, 0.0);
			if (line > 0.333)
				mask = vec3(0.0, 3.0, 0.0);
			if (line > 0.666)
				mask = vec3(0.0, 0.0, 3.0);
			
			COLOR.xyz *= mask;
			
		}
		if(!transparency_to_black)
			COLOR.a = texture(TEXTURE, UV).a;
	} else {
		COLOR = texture(TEXTURE, UV);
	}
}"

[resource]
shader = SubResource( 1 )
shader_param/enable_glitch = true
shader_param/transparency_to_black = false
shader_param/block_thresh_modifier = 0.25
shader_param/line_thresh_modifier = 0.45
shader_param/block_dist_modifier = 0.35
shader_param/iChannel1 = ExtResource( 1 )
